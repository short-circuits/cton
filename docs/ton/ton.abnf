ton = white-space ton-value white-space
ton-value             = [ type-identifer ] ton-bare-value
type-identifer        = type-identifer-mark [ *( type-identifer-char )] type-identifer-ending
type-identifer-mark   = "!"
type-identifer-ending = " " / type-separator
type-separator        = ( ":" / non-break-white-space ) [ *( non-break-white-space ) ]
ton-bare-value        = null-value / boolean-value / numeric-value / string / array / object
boolean-value         = true-value / false-value
white-space           = [ *( non-break-white-space / comment / line-break ) ]
line-break            = %x0a / %x0d [ %x0a ]
null-value            = "null"
true-value            = "true"
false-value           = "false"
non-break-white-space = [ *( " " / %x09 ) ]
string                = quoted-string / unquoted-string
array-begin           = white-space "[" white-space
array-end             = white-space "]" white-space
object-begin          = white-space "{" white-space
object-end            = white-space "}" white-space
value-seperator       = white-space "," white-space
name-seperator        = white-space ":" white-space
array                 = array-begin [ ton-value [ *( value-seperator ton-value ) ] ] array-end
object-member         = string name-seperator ton-value
object                = object-begin [ object-member [ *( value-seperator object-member ) ] ] object-end
comment-mark          = "#"
comment               = comment-mark [ *non-break-char ] line-break
numerical-sign        = "-" / "+"
numerical-frac        = "." [ *number ]
numerical-exp         = ( "e" / "E" ) [ numerical-sign ] integer
numeric-value         = [ numerical-sign ] ( numerical-frac / ( integer [ numerical-frac ] ) ) [ numerical-exp ]
integer               = *number
string-escape         = %x5c ( %x22 / %x5c / "/" / "b" / "n" / "r" / "t"
                          / ( "u" 4hex-digit ) / ( "x" 2hex-digit) )
unquoted-string       = *( unquoted-string-char / string-escape )
quoted-string         = %x22 *( quoted-string-char / string-escape ) %x22
type-identifer-char   = letter / number / ascii-non-space-ctl / non-ascii / "-" / "."
unquoted-string-char  = letter / number / ascii-non-space-ctl / non-ascii / ascii-non-tbon-mark / "+" / %x0d / %x0a / %x09 / " "
quoted-string-char    = unquoted-string-char / "[" / "]" / "{" / "}" / "," / ":" / "!" / "-" / "."
non-break-char        = type-identifer-char / ascii-non-tbon-mark / non-break-white-space
                          / "[" / "]" / "{" / "}" / "," / ":" / "!"
url-safe-char         = letter / number / "-" / "_" / "." / "~"
letter                = lower-letter / upper-letter
ascii-non-space-ctl   = %x00-08 / %x0b-0c / %x0e-1f / %x7F
hex-digit             = number / %x41-46 / %x61-66
ascii-non-tbon-mark   = "$" / "%" / "&" / "'" / "(" / ")" / "*" / "<" / "=" / ">" / "?" / "@" / "^" / "`" / "|" / "-" / "~" / "/" / ";"
number                = %x30-39
upper-letter          = %x41-5A
lower-letter          = %x61-7A
non-ascii             = %x80-FF
